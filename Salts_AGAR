//***********************************************************************
//AGAR salts analysis for high-magnification SEM images

//The macro segments background, salts and adhering particles,
//then measures the size, shape and coverage (area %) of the salts.
//Output files are saved in a folder using the image file name as title.
//MorphoLibJ, Shape Filter and Read and Write Excel plugins are required.

//See the related publication for details:
//Casas, Hornby, Cimarelli and Dingwell 2020 - "An imaging tool for 
//rapid quantitative morphological characterization of soluble salts 
//on ash particles"
//***********************************************************************

macro "Salts AGAR 18K" {	
//***********************************************************************
//Functions
//***********************************************************************
	
//reduce noise and set the  threshold for surface particles
	function manualthreshold() { 
		selectImage(cropped);
		cropped=getTitle();
		run("Duplicate...", " ");
		particles1=getImageID();
		
		Dialog.create("Smoothing and threshold options");
		Dialog.addCheckbox("run Non-local means denoising?", 1);
		gThr="Set a global threshold for all particles?";
		dThr="Set light and dark thresholds for particles?";
		tThr="Set a separate threshold for non-target particles?";
		itemsT=newArray(gThr, tThr, dThr);
		Dialog.addChoice("threshold options", itemsT, "Set a global threshold for all particles");
		Dialog.show();
		nlmd=Dialog.getCheckbox();
		ThrOp=Dialog.getChoice();
	
		if (nlmd==1)
			run("Non-local Means Denoising", "sigma=15 smoothing_factor=1 auto");	
		
		run("Add Image...", "image=[cropped] x=0 y=0 opacity=70");
		if (ThrOp==dThr) {
			run("Threshold...");
			waitForUser("Set dark threshold", "so that all pixels darker than the background are black...\n...then click \"Apply\"");
			run("Convert to Mask");
			while (is("binary")==0 ) {
				waitForUser("Threshold not applied!", "Click Apply in the Threshold window then click OK to continue");
			}
			run("Close");
			selectImage(cropped);
			run("Duplicate...", " ");
			particles2=getImageID();
			run("Threshold...");
			waitForUser("Set light threshold", "so that all pixels lighter than the background are white...\n...then click \"Apply\"");
			//run("Convert to Mask");
			//setThreshold(255, 255);
			while (is("binary")==0 ) {
				waitForUser("Threshold not applied!", "Click Apply in the Threshold window then click OK to continue");
			}
			imageCalculator("Add create", particles1, particles2);
			selectImage(particles1);
			close();
			selectImage(particles2);
			close();
		}

		else if (ThrOp==tThr) {
			run("Threshold...");
			waitForUser("Set non-target threshold", "so that all target pixels are white and background is black...\n...then click \"Apply\"");
			run("Convert to Mask");
			while (is("binary")==0 ) {
				waitForUser("Threshold not applied!", "Click Apply in the Threshold window then click OK to continue");
			}
			run("Close");
			rename("xtalsthr");
			selectImage(cropped);
			run("Duplicate...", " ");
			particles2=getImageID();
			run("Threshold...");
			waitForUser("Set light threshold", "so that all pixels lighter than the background are white...\n...then click \"Apply\"");
			run("Convert to Mask");
			while (is("binary")==0 ) {
				waitForUser("Threshold not applied!", "Click Apply in the Threshold window then click OK to continue");
			}
			run("Close");
		}
		else {
			run("Threshold...");
			waitForUser("Set light threshold", "so that all pixels lighter than the background are white...\n...then click \"Apply\"");
			//run("Convert to Mask");
			while (is("binary")==0 ) {
				waitForUser("Threshold not applied!", "Click Apply in the Threshold window then click OK to continue");
			}
			run("Close");
		}
		
	}
	
//manual editing of threshold area
	function fillparticles() {
		prefill=getTitle();
		selectImage(cropped);
		cropped=getTitle();
		wait(50);
		selectImage(prefill);
		run("Add Image...", "image=[cropped] x=0 y=0 opacity=70");
		setForegroundColor(255, 255, 255);
		setTool(19);
		waitForUser("Use Paintbrush to define "+particles+" outlines (if necessary)..., then press OK");
		if (holes==1)
			run("Fill Holes");
		Dialog.create("Particles");
		Dialog.addCheckbox("Are all "+particles+" filled?", 0);
		Dialog.show();
		if (Dialog.getCheckbox()==0) {
			Overlay.remove;
			fillparticles();	
		}
	}

//Salts size, shape and area fraction calculation
//also provided as a standalone macro, salts_shape_filter.txt
	function salt_measurements() {
	run("Clear Results");
	open(path7);
	croparea=getImageID();
	selectImage(croparea);
	run("Make Binary");
	run("Measure");
	imagearea=getValue("Area");
	print("image area = " + imagearea);
	run("Clear Results");
	selectImage(xtals);
	run("Invert");
	Overlay.remove;
	run("Make Binary");
	wait(50);
	run("Analyze Particles...", "size=5-Infinity pixel display");
	sumxA=0;
	for (k=0; k<nResults; k++) {
		sumxA=sumxA+getResult("Area", k);
	}
	xtalarea=sumxA;
	print("xtal area = " + xtalarea);
	run("Clear Results");
	selectImage(salts);
	run("Make Binary");
	wait(50);
	run("Analyze Particles...", "size=5-Infinity pixel display");
	sumsA=0;
	for (k=0; k<nResults; k++) {
		sumsA=sumsA+getResult("Area", k);
	}
	salts=sumsA;
	print ("salts area = " + salts);
	saltcanvas=(imagearea-xtalarea);
	print("salt canvas area = " + saltcanvas);
	saltspercent=((salts/saltcanvas)*100);
	print ("Total salts coverage (%) = " + saltspercent);
	run("Clear Results");
	run("Make Binary");
	//run("Invert");
	Dialog.create("Edges");
	Dialog.addCheckbox("Exclude particles on edges from single-particle measurements?", 1);
	Dialog.show();
	if (Dialog.getCheckbox()==1)
		run("Shape Filter", "area=0-Infinity area_convex_hull=0-Infinity perimeter=0-Infinity perimeter_convex_hull=0-Infinity feret_diameter=0-Infinity min._feret_diameter=0-Infinity max._inscr._circle_diameter=0-Infinity area_eq._circle_diameter=0-Infinity long_side_min._bounding_rect.=0-Infinity short_side_min._bounding_rect.=0-Infinity aspect_ratio=1-Infinity area_to_perimeter_ratio=0-Infinity circularity=0-Infinity elongation=0-1 convexity=0-1 solidity=0-1 num._of_holes=0-Infinity thinnes_ratio=0-1 contour_temperatur=0-1 orientation=0-180 fractal_box_dimension=0-2 option->box-sizes=2,3,4,6,8,12,16,32,64 add_to_manager draw_holes black_background fill_results_table exclude_on_edges");
	else
		run("Shape Filter", "area=0-Infinity area_convex_hull=0-Infinity perimeter=0-Infinity perimeter_convex_hull=0-Infinity feret_diameter=0-Infinity min._feret_diameter=0-Infinity max._inscr._circle_diameter=0-Infinity area_eq._circle_diameter=0-Infinity long_side_min._bounding_rect.=0-Infinity short_side_min._bounding_rect.=0-Infinity aspect_ratio=1-Infinity area_to_perimeter_ratio=0-Infinity circularity=0-Infinity elongation=0-1 convexity=0-1 solidity=0-1 num._of_holes=0-Infinity thinnes_ratio=0-1 contour_temperatur=0-1 orientation=0-180 fractal_box_dimension=0-2 option->box-sizes=2,3,4,6,8,12,16,32,64 add_to_manager draw_holes black_background fill_results_table");
	
	n = nResults;
	area1 = newArray(n); 
	length1 = newArray(n); 
	area2 = newArray(n); 
	length2 = newArray(n);
	ff1 = newArray(n);
	feret1 = newArray(n); 
	minferet1 = newArray(n);
	orientation1 = newArray(n);
	roiLabel1 = newArray(n);
	
	for (j = 0; j<n; j++) {
		orientation1[j] = getResult('Orientation', j); 
		length1[j] = getResult('Peri.', j);
		area1[j] = getResult('Area', j);
		area2[j] = getResult('Area Conv. Hull', j);
		length2[j] = getResult('Peri. Conv. Hull', j);
		ff1[j] = getResult('Thinnes Rt.', j);
		feret1[j] = getResult('Feret', j);
		minferet1[j] = getResult('Min. Feret', j);
	}
	
	run("Clear Results"); 
	for (j = 0; j<n; j++) { 
		setResult("Area", j, area1[j]);
		setResult("Perim.", j, length1[j]); 
		setResult("CH Area", j, area2[j]); 
		setResult("CH Perim.", j, length2[j]); 
		setResult("Solidity", j, area1[j]/area2[j]); 
		setResult("Convexity", j, length2[j]/length1[j]); 
		setResult("FormFactor", j, ff1[j]);
		setResult("Circularity", j, length1[j]/(2*sqrt(PI*area1[j])));
		setResult("Roundness", j, 4*area1[j]/(PI*pow(feret1[j],2)));
		setResult("AR feret", j, minferet1[j]/feret1[j]);
		setResult("Feret d", j, feret1[j]); 
		setResult("MinFeret d", j, minferet1[j]);
		setResult("Orientation", j, orientation1[j]);
	} 
		
	updateResults();
	selectWindow("Results");
	saveAs("results", path5);
	run("Summarize");
	headings = split(String.getResultsHeadings);
	print(String.getResultsHeadings);
	for (b = nResults-4; b<nResults; b++) {
		row=b;
		line = "";
		for (a=0; a<lengthOf(headings); a++)
    		line = line + getResult(headings[a],row) + " ";
			print(line);
	}
	print ("Mean");
	print("s.d.");
	print("min");
	print("max");
	selectWindow("Log");
	save(path6);
}

	function disconnectParticles () {
		Dialog.create("Disconnect particles");
		Dialog.addCheckbox("Separate touching particles?", 1);
		Dialog.addNumber("Disconnect level 0-1 (1 = max. disconnects, 0.8 = default)", "0.8");
		Dialog.show();
		dc=Dialog.getCheckbox();
		dcl=Dialog.getNumber();
		if(dc==1) {
			run("Disconnect Particles", "disconnection=dcl xsize=1.0000 ysize=1.0000 zsize=1.0000 algorithm=[new algorithm] euler=[  8] sigma=1,1,1 distance=0.5000 holes,=0.0000 particles,=0.0000 separate");
			wait(1000);
			selectWindow("labeled");
			setAutoThreshold("Default dark");
			setOption("ScaleConversions", true);
			run("8-bit");
			setAutoThreshold("Default dark");
			run("Threshold...");
			setThreshold(1, 255);
			setOption("BlackBackground", true);
			run("Convert to Mask");
			//run("Close");
			run("Set Scale...", "distance="+scaleL+" known="+scale+" pixel=1 unit=um");
			Dialog.create("Disconnect check");
			Dialog.addCheckbox("Accept particle separation? (leave unticked to change disconnection level)", 0);
			Dialog.show();
			adc=Dialog.getCheckbox();
			if (adc==0) {
				selectWindow("labeled");
				close();
				disconnectParticles();
			}
		}
	}
	
//****************************************************************
//End of functions
//****************************************************************

	
//clear, reset and open images
//**************************************
	run("Clear Results"); 
	if (nImages>0) 
		close("*");
	print("\\Clear");
	setOption("BlackBackground", true);
	
	Dialog.create("Single or batch");
	Dialog.addCheckbox("Batch process a set of images in a folder?", 0);
	Dialog.addMessage("Leave unchecked to run on a single image");
	Dialog.show();
	runtype=Dialog.getCheckbox();
	if (runtype==1) {
		input=getDirectory("Choose parent directory");
		output=getDirectory("Choose or create output directory");
		list=getFileList(input);
		Array.print(list);
	}
	else { input=File.openDialog("Choose image to be processed");
		output=getDirectory("Choose or create output directory");
		list=newArray(input);
	}
	
	for (i = 0; i < list.length; i++) {
		if (runtype==0) 
			open(input);
		else { dirName = list[i];
			dir=input+File.separator+dirName;
			if (endsWith(list[i], "/"))
				continue
			if (endsWith(list[i], ".txt"))
				continue
			open(dir);
		}	
		
		sampleName=File.nameWithoutExtension;
		savedir=output+File.separator+sampleName;
		orig=getTitle();
		File.makeDirectory(savedir)
		path1=savedir+File.separator+"-crop.tif";
		path2=savedir+File.separator+"-salts and xtals.tif";
		path3=savedir+File.separator+"-xtals.tif";
		path4=savedir+File.separator+"-salts.tif";
		path5=savedir+File.separator+"-results.csv";
		path6=savedir+File.separator+"-log.txt";
		path7=savedir+File.separator+"-croparea.tif";
		
//scale, crop and threshold image
//**************************************	
		setTool("line");
		Dialog.create("Scale")
		Dialog.addMessage("Enter length of scale bar (numbers only)");
		Dialog.addNumber("scale bar length", 10);
		Dialog.show();
		scale=Dialog.getNumber();
		waitForUser("Measure scale... then press OK");
		getLine(x1, y1, x2, y2, lineWidth);
		scaleL=x2-x1;
		run("Set Scale...", "known="+scale+" pixel=1 unit=um");
		
		setTool("rectangle");
		run("Duplicate...", " ");
		bandpass=getImageID();
		waitForUser("Select area to measure (or crop out scale text)... then press OK");	
		run("Bandpass Filter...", "filter_large=50 filter_small=3 suppress=None tolerance=5 autoscale saturate");
		
		Dialog.create("Use FFT bandpass?");
		items = newArray("Yes (with default settings, as shown)", "Yes (but with manual settings)", "No");
		Dialog.addRadioButtonGroup("Use bandpass (previewed in selection)?", items, 3, 1, "Yes (with default settings, as shown)");
		Dialog.show();
		fft=Dialog.getRadioButton();
		if (fft=="Yes (with default settings, as shown)") {
			run("Crop");
		}
		if (fft=="Yes (but with manual settings)") {
			roiManager("add");
			close();
			selectWindow(orig);
			roiManager("Select", 0);
			run("Bandpass Filter...");
			run("Crop");
		}
		if (fft=="No") {
			roiManager("add");
			close();
			selectWindow(orig);
			roiManager("Select", 0);
			run("Crop");
		}
		roiManager("reset");
		save(path1);
		run("Duplicate...", " ");
		run("8-bit");
		rename("cropped");
		run("Duplicate...", " ");
		setForegroundColor(0, 0, 0);
		run("Select All");
		run("Fill");
		croparea=getImageID();
		save(path7);
		close(orig);
		selectWindow("cropped");
		cropped=getImageID();

//roughly define size difference between salts and particles
//**************************************
		Dialog.create("Particle size threshold");
		Dialog.addCheckbox("Select particle size?", 1);
		Dialog.addMessage("Check if image contains particles that are larger\n"
		+"than the component of interest");
		Dialog.show();
		if (Dialog.getCheckbox()==1) {
			adhparts=1;
			run("Duplicate...", " ");
			run("Select None");
			setTool("freehand");
			waitForUser("Outline smallest adhering non-salt surface particle");
			minPsize=getValue("Area")/1.5;
			close();
		}
		else
			minPsize=0;
			adhparts=0;
//thresholding
//**************************************
		manualthreshold();
		
		Dialog.create("Checks");
		Dialog.addMessage("Check that background is black and particles are white");
		Dialog.addCheckbox("If not, select here", 0);
		Dialog.addMessage(" \n"
		+"...then click OK");
		Dialog.addCheckbox("Fill holes in particles?", 1);
		Dialog.show();
		
		if (Dialog.getCheckbox()==1)
			run("Invert");
		if (Dialog.getCheckbox()==1) {
			run("Fill Holes");
			holes=1;
			partholes=getImageID();
			run("Morphological Filters", "operation=Closing element=Diamond radius=1");
			pmanclose=getImageID();
			run("Fill Holes");
			run("Median...", "radius=2");
			run("Morphological Filters", "operation=Dilation element=Diamond radius=1");
			saltsandxtals=getImageID();
			save(path2);
			if (isOpen("xtalsthr")){
				selectWindow("xtalsthr");
				run("Morphological Filters", "operation=Closing element=Diamond radius=1");
				pmanclose2=getImageID();
				run("Fill Holes");
				run("Median...", "radius=2");
				run("Morphological Filters", "operation=Dilation element=Diamond radius=1");
				xtalsthr2=getImageID();
				selectimage(xtalsthr);
				close();
				selectimage(pmanclose2);
				close;
				selectImage(saltsandxtals);
			}
			run("Make Binary");
			run("Create Selection");
			run("Create Mask");
			xtalsI=getImageID();
			selectImage(partholes);
			close();
			selectImage(pmanclose);
			close();
		}
	
		else {
			holes=0;
			run("Morphological Filters", "operation=Opening element=Diamond radius=1");
			pmanopen=getImageID();
			run("Median...", "radius=2");
			run("Morphological Filters", "operation=Dilation element=Diamond radius=1");
			saltsandxtals=getImageID();
			save(path2);
			if (isOpen("xtalsthr")){
				selectWindow("xtalsthr");
				run("Morphological Filters", "operation=Opening element=Diamond radius=1");
				pmanclose2=getImageID();
				run("Median...", "radius=2");
				run("Morphological Filters", "operation=Dilation element=Diamond radius=1");
				rename("xtalsthr2");
				selectWindow("xtalsthr");
				close();
				selectImage(pmanclose2);
				close;
				selectImage(saltsandxtals);
			}
			run("Create Selection");
			run("Create Mask");
			xtalsI=getImageID();
			selectImage(pmanopen);
			close();
		}

//manual corrections to particles
//*************************************	
		//roiManager("reset");
		particles = "adhering (non-salt) particles";
		if (isOpen("xtalsthr2"))
			selectWindow("xtalsthr2");
		
		run("Analyze Particles...", "size=minPsize-Infinity show=Masks");
		run("Invert");
		fillparticles();
//save files
//**************************************
		xtals=getImageID();
		saveAs(path3);
		run("Invert");
		open(path2);
		saltsandxtals=getTitle();
		imageCalculator("subtract create", saltsandxtals, xtals);
		//waitForUser("image calculator");
		//run("Analyze Particles...", "size=5-Infinity pixel show=Masks");
		//waitForUser ("analyze particles - masks");
		//run("Invert");
		particles="salt crystals";
		fillparticles();
		disconnectParticles();
		particles="disconnected salts";
		holes=0;
		fillparticles();	
		saveAs(path4);
		salts=getImageID();
	
//run calculations
//**************************************	
		print("\\Clear");
		salt_measurements();
		close("*");
		print("\\Clear");
		close("Results");		
	}
}
